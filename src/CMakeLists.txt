

EchoWithColor(COLOR GREEN "-- Configure ${CMAKE_CURRENT_LIST_DIR}")

set (PROJECT_LIBATAPP_LIB_LINK atapp)
set (PROJECT_LIBATAPP_ROOT_SRC_DIR ${CMAKE_CURRENT_LIST_DIR})

list(APPEND PROJECT_LIBATAPP_PUBLIC_INCLUDE_DIRS ${PROJECT_LIBATAPP_ROOT_INC_DIR})
file(RELATIVE_PATH PROJECT_ROOT_RELINC_DIR ${CMAKE_CURRENT_LIST_DIR} ${PROJECT_LIBATAPP_ROOT_INC_DIR})

file(GLOB_RECURSE PROJECT_LIB_SRC_LIST
    ${PROJECT_ROOT_RELINC_DIR}/*.h
    ${PROJECT_ROOT_RELINC_DIR}/*.hpp
    ${PROJECT_ROOT_RELINC_DIR}/*.hxx
    ${PROJECT_LIBATAPP_ROOT_SRC_DIR}/*.h
    ${PROJECT_LIBATAPP_ROOT_SRC_DIR}/*.hpp
    ${PROJECT_LIBATAPP_ROOT_SRC_DIR}/*.c
    ${PROJECT_LIBATAPP_ROOT_SRC_DIR}/*.cpp
    ${PROJECT_LIBATAPP_ROOT_SRC_DIR}/*.cc
    ${PROJECT_LIBATAPP_ROOT_SRC_DIR}/*.cxx
)
source_group_by_dir(PROJECT_LIB_SRC_LIST)

# ============ libatbus - src ============
if(BUILD_SHARED_LIBS)   
    add_library(${PROJECT_LIBATAPP_LIB_LINK} SHARED ${PROJECT_LIB_SRC_LIST})
    target_compile_definitions(${PROJECT_LIBATAPP_LIB_LINK}
        PRIVATE LIBATAPP_MACRO_API_NATIVE=1 LIBATAPP_MACRO_API_DLL=1
    )
    set_target_properties(${PROJECT_LIBATAPP_LIB_LINK} PROPERTIES INTERFACE_COMPILE_DEFINITIONS LIBATAPP_MACRO_API_DLL=1)
else ()
    add_library(${PROJECT_LIBATAPP_LIB_LINK} STATIC ${PROJECT_LIB_SRC_LIST})
    target_compile_definitions(${PROJECT_LIBATAPP_LIB_LINK}
        PRIVATE LIBATAPP_MACRO_API_NATIVE=1
    )
endif()

set_target_properties(${PROJECT_LIBATAPP_LIB_LINK} PROPERTIES 
    C_VISIBILITY_PRESET "hidden"
    CXX_VISIBILITY_PRESET "hidden"
)

target_link_libraries(${PROJECT_LIBATAPP_LIB_LINK}
    PUBLIC ${PROJECT_LIBATAPP_PUBLIC_LINK_NAMES}
    INTERFACE ${PROJECT_LIBATAPP_INTERFACE_LINK_NAMES}
)

target_include_directories(${PROJECT_LIBATAPP_LIB_LINK} 
    PUBLIC 
        "$<BUILD_INTERFACE:${PROJECT_LIBATAPP_PUBLIC_INCLUDE_DIRS}>"
        "$<INSTALL_INTERFACE:include>"
    PRIVATE 
        "$<BUILD_INTERFACE:${PROJECT_LIBATAPP_ROOT_SRC_DIR}>"
)

if (PROJECT_LIBATAPP_PRIVATE_COMPILE_OPTIONS)
    target_compile_options(${PROJECT_LIBATAPP_LIB_LINK} 
        PRIVATE ${PROJECT_LIBATAPP_PRIVATE_COMPILE_OPTIONS}
    )
endif ()

install(TARGETS ${PROJECT_LIBATAPP_LIB_LINK}
    EXPORT ${PROJECT_LIBATAPP_EXPORT_NAME}
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
)

install(DIRECTORY ${PROJECT_LIBATAPP_ROOT_INC_DIR}
    DESTINATION .
    FILES_MATCHING REGEX ".+\\.h(pp)?$"
    PATTERN ".svn" EXCLUDE
    PATTERN ".git" EXCLUDE
)

list(APPEND PROJECT_LIBATAPP_EXPORT_TARGETS ${PROJECT_LIBATAPP_LIB_LINK})
